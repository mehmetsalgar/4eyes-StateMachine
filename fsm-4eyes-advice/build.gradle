plugins {
    id("java-common-conventions")
    id 'org.xtext.xtend' version '4.0.0'
    id 'org.xtext.builder' version '4.0.0'
    id 'com.diffplug.spotless' version '8.0.0'
    id 'io.freefair.aspectj' version '9.0.0'
    id 'io.freefair.lombok' version '8.14.1'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated-sources/main/java']
            include '**/*.java', '**/*.aj'
        }
    }
}

dependencies {
    implementation libs.aspectj.rt

    implementation libs.pekko.actor.typed
    implementation libs.pekko.persistence.typed

    implementation libs.logback
}

spotless {
    java {
        target '**/*.java'
        importOrder 'java', 'javax', 'org', 'com'
    }
    format 'misc', {
        target 'src/**/*.md', 'src/**/*.xml', 'src/**/*.xsd', 'src/**/*.xsl'
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

configurations {
    mwe2 {
        extendsFrom implementation
    }
}

dependencies {
    mwe2 libs.fsm.text.reader
    mwe2 libs.fsm.xtend
    mwe2 project(':fsm-4eyes-uml-model')
    mwe2 'org.salgar.pekko.fsm:fsm-pekko-eclipse-dependencies:2.0.0-SNAPSHOT:repackaged'
    mwe2 libs.javax.inject
    mwe2 libs.reflections
}

tasks.register('generateXtextLanguage', JavaExec) {
    def generatedFileDir = file("target/generated-sources")
    outputs.dir generatedFileDir
    mainClass = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = configurations.mwe2
    inputs.file('src/main/mwe2/GenerateWorkflow.mwe2').withPathSensitivity(PathSensitivity.RELATIVE)
    outputs.dir('build/generated-sources')
    args += "src/main/mwe2/GenerateWorkflow.mwe2"
    args += "-p"
    args += "rootPath=/${projectDir}/.."
    args += "-p"
    args += "umlModel=${UML_MODEL}"
    args += "-p"
    args += "projectPackage=${PROJECT_PACKAGE}"
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
spotlessJava.dependsOn(generateXtextLanguage)
compileJava.dependsOn(spotlessApply)
compileJava.dependsOn(generateXtextLanguage)