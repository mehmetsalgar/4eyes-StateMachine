plugins {
    id("java-common-conventions")
    id 'org.xtext.xtend' version '4.0.0'
    id 'org.xtext.builder' version '4.0.0'
    id 'com.google.protobuf' version '0.9.5'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated/sources/proto/main/java']
        }
        proto {
            srcDir 'build/generated/main/proto'
        }
    }
}

dependencies {
    //implementation 'com.google.protobuf:protobuf-lite:3.0.0'
    implementation libs.protobuf.java
}

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:4.33.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.dependsOn{ generateXtextLanguage }
        }
    }
}

configurations {
    mwe2 {
        extendsFrom implementation
    }
}

dependencies {
    mwe2 libs.fsm.text.reader
    mwe2 libs.fsm.xtend
    mwe2 project(':fsm-4eyes-uml-model')
    mwe2 'org.salgar.pekko.fsm:fsm-pekko-eclipse-dependencies:2.0.0-SNAPSHOT:repackaged'
    mwe2 libs.javax.inject
    mwe2 libs.reflections
}

tasks.register('generateXtextLanguage', JavaExec) {
    def generatedFileDir = file("src/generated")
    outputs.dir generatedFileDir
    mainClass = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = configurations.mwe2
    inputs.file('src/main/mwe2/GenerateWorkflow.mwe2').withPathSensitivity(PathSensitivity.RELATIVE)
    args += "src/main/mwe2/GenerateWorkflow.mwe2"
    args += "-p"
    args += "rootPath=/${projectDir}/.."
    args += "-p"
    args += "umlModel=${UML_MODEL}"
}

generateXtext.dependsOn(generateXtextLanguage)
clean.dependsOn(cleanGenerateXtextLanguage)
generateProto.dependsOn(generateXtextLanguage)
compileJava.dependsOn(generateXtextLanguage)
compileJava.dependsOn(generateProto)